apply plugin: 'maven-publish'
apply plugin: 'signing'

    def gitBranch() {
        def branch = ""
        def proc = "git rev-parse --abbrev-ref HEAD".execute()
        proc.in.eachLine { line -> branch = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
        branch
    }

afterEvaluate {
    def branchName = gitBranch()
    branchName.replaceAll('-', '_')

    if (project.hasProperty("SNAPSHOT")) {
        version += "-SNAPSHOT"
    } else if (project.hasProperty("LOCAL")) {
        if (!branchName.equals("master")) {
           version += '-' + (branchName.replaceAll('-', '_'))
        }

        version += "-LOCAL"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = POM_NAME
                    description = POM_DESCRIPTION
                    url = 'https://github.com/VabrantGames/ActionSystem'

                    licenses {
                        license {
                            name = "Apache license 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "VabrantGames"
                            name = "John Barton"
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com:VabrantGames/ActionSystem.git'
                        developerConnection = 'scm:git@github.com:VabrantGames/ActionSystem.git'
                        url = 'https://github.com/VabrantGames/ActionSystem'
                    }
                }
            }

        }

        repositories {
            maven {
                url = isRelease() ? getReleaseRepositoryURL() : getSnapshotRepositoryURL()

                credentials {
                    username = findProperty("sonatypeUsername")
                    password = findProperty("sonatypePassword")
                }
            }
        }
    }
    signing {
        sign publishing.publications.mavenJava
    }
}
